plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.7.1"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.20.0"
        }
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove javanone
            }
            task.plugins {
                grpc {
                    option("lite")
                }
                javalite { }
            }
        }
    }
}

android {
    namespace 'android.car'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            Set<File> fileSet = options.bootstrapClasspath.getFiles()
            List<File> newFileList = new ArrayList<>();
            newFileList.add(new File('../libs/framework.jar'))
            newFileList.add(new File('../libs/framework-wifi.jar'))
            newFileList.addAll(fileSet)
            options.bootstrapClasspath = files(newFileList.toArray())
        }
    }
    sourceSets {
        main {
            aidl.srcDirs = ['src/main/java']
            proto {
                srcDir 'src/main/java'
            }
        }
    }
}

dependencies {
    compileOnly files("${rootProject.projectDir}/libs/framework.jar")
    compileOnly files("${rootProject.projectDir}/libs/framework-bluetooth.jar")
    compileOnly files("${rootProject.projectDir}/libs/framework-wifi.jar")
    compileOnly files("${rootProject.projectDir}/libs/core-libart.jar")

    implementation project(':vehicle-libs')
    implementation project(':car-builtin-lib')

    api 'com.google.protobuf:protobuf-lite:3.0.1'
}
